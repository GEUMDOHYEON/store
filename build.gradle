plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
	// gRPC통신 시에 사용되는 protobuf 플러그인 설정
	id 'com.google.protobuf' version '0.9.4'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	// gRPC
	implementation 'io.grpc:grpc-netty-shaded:1.66.0'
	implementation 'io.grpc:grpc-protobuf:1.66.0'
	implementation 'io.grpc:grpc-stub:1.66.0'
	implementation 'com.google.protobuf:protobuf-java:4.28.2'
	implementation "com.google.protobuf:protobuf-java-util:4.27.4"

	// gRPC Client
	implementation 'net.devh:grpc-client-spring-boot-starter:3.1.0.RELEASE'

	// ProtoBuf
	compileOnly 'javax.annotation:javax.annotation-api:1.3.2'

	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'com.h2database:h2'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// gRPC 및 Protobuf 설정
protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:4.28.2"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:1.66.0"
		}
	}
	generateProtoTasks {
		all().each { task ->
			task.plugins {
				grpc {}
			}
		}
	}
}
// 프로토콜 버퍼 파일 경로 설정
sourceSets {
	main {
		proto {
			srcDir 'src/main/proto'
		}
		java {
			// 프로토콜 파일로부터 생성된 코드 경로를 포함
			srcDirs += 'build/generated/source/proto/main/java'
			srcDirs += 'build/generated/source/proto/main/grpc'
		}
	}
}

// 컴파일 태스크가 Protobuf 파일 생성을 포함하도록 의존성 설정
compileJava {
	dependsOn 'generateProto'
}

// Protobuf 관련 경로 정리
clean {
	delete 'build/generated'
}
tasks.processResources {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}


tasks.named('test') {
	useJUnitPlatform()
}
